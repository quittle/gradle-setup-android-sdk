buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '4.7.0'
    id 'com.gradle.plugin-publish' version '0.13.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    jcenter()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget('HEAD')
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        setupAndroidSdkPlugin {
            id = 'com.quittle.setup-android-sdk'
            implementationClass = 'com.quittle.setupandroidsdk.SetupAndroidSdkPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'setup-android-sdk'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-setup-android-sdk'
    vcsUrl = 'https://github.com/quittle/gradle-setup-android-sdk'
    description = 'Automatically installs Android SDK dependencies for Android Gradle builds.'
    tags = ['android', 'android sdk', 'installer']

    plugins {
        setupAndroidSdkPlugin {
            displayName = 'Setup Android SDK'
        }
    }
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:4.1.3'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.14.0'

    annotationProcessor 'com.google.code.findbugs:annotations:3.0.1'
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    implementation 'org.junit.jupiter:junit-jupiter:5.7.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.code.findbugs:annotations:3.0.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352

    if(JavaVersion.current() != JavaVersion.VERSION_1_8){
        options.addBooleanOption 'html5', true
    }
}

pmd {
    incrementalAnalysis = true
}

spotbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
