buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
    }
}

import org.eclipse.jgit.api.Git

plugins {
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'pmd'
}

repositories {
    google()
    mavenCentral()
}

group = 'com.quittle'
version = Git.open(project.rootDir.parentFile)
        .describe()
        .setTarget('HEAD')
        .setTags(true)
        .call()

gradlePlugin {
    plugins {
        setupAndroidSdkPlugin {
            id = 'com.quittle.setup-android-sdk'
            displayName = 'Setup Android SDK'
            description = 'Automatically installs Android SDK dependencies for Android Gradle builds.'
            implementationClass = 'com.quittle.setupandroidsdk.SetupAndroidSdkPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'setup-android-sdk'
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/quittle/gradle-setup-android-sdk'
    vcsUrl = 'https://github.com/quittle/gradle-setup-android-sdk'
    tags = ['android', 'android sdk', 'installer']
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:7.3.1'
    implementation 'commons-io:commons-io:2.11.0'

    annotationProcessor 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.7.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Werror'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

javadoc {
    failOnError true
    options.addStringOption 'Xwerror', '-quiet' // See https://stackoverflow.com/a/49544352
    exclude 'com/quittle/setupandroidsdk/AndroidRevision.java'

    if(JavaVersion.current() != JavaVersion.VERSION_1_8){
        options.addBooleanOption 'html5', true
    }
}

pmd {
    incrementalAnalysis = true
}

spotbugs {
    effort 'max'
    reportLevel 'low'
}

defaultTasks 'assemble', 'javadoc', 'check'
